#!/usr/bin/env perl
use App::Cope;
use Regexp::Common qw[net];

sub ping_time {
  my ($ms) = @_;
  $ms =~ s/ ms$//g;
  given ($ms) {
    when ( $_ >= 200 ) { return 'red bold' }
    when ( $_ >= 100 ) { return 'yellow bold' }
    default            { return 'green bold' }
  }
}

sub pct {
  my ($num) = @_;
  $num =~ s/^(\d+).+/$1/;	# extract number
  given ($num) {
    when ( $_ > 25 ) { return 'red bold' }
    when ( $_ > 0  ) { return 'yellow bold' }
    when ( $_ == 0 ) { return 'green bold' }
  }
}

sub process {

  # header
  line qr{^PING (\S+)} => 'blue bold underline';

  # ping replies
  line qr{time=([0-9.]+ ms)\b}       => \&ping_time;
  line qr{(?:icmp_seq|ttl)=(\d+)}    => 'bold';
  line qr{^(?:\d+ bytes )?[Ff]rom ([^:\s]+)} => 'blue bold';
  mark qr{\($RE{net}{IPv4}\)}        => 'blue';

  # final ping statistics
  line qr{(\d+% packet loss)} => \&pct;
  line qr{min/avg/(?:max/mdev|ewma/max)\s=\s
	  ([0-9.]+)/    # minimum
	  ([0-9.]+)/    # average
	  ([0-9.]+)/    # maximum or moving average
	  ([0-9.]+\sms) # mean deviation or maximum
       }x => ( \&ping_time ) x 4;

}

run( \&process, real_path, @ARGV );
