#!/usr/bin/env perl
use App::Cope;
use v5.10;

my @headers = qw[USER TTY FROM LOGIN@ IDLE JCPU PCPU WHAT];

my $what_pos = 0;
my $me = (getpwuid( $< ))[0] || "nobody";
my $using_from = 0;

sub process {
  if (/^USER/) {
    for my $h (@headers) { mark $h => 'underline'; }
    $what_pos   = $-[0] if /WHAT/;
    $using_from = 1     if /FROM/;
  }
  else {
    line qr{^(\w+)} => sub { ( shift eq $me ) ? 'yellow bold' : 'yellow' };

    line qr{^\w+\s+(vc)(/\d+)}  => 'blue',    'blue bold';
    line qr{^\w+\s+(pts)(/\d+)} => 'green',   'green bold';
    line qr{^\w+\s+(tty)(\d+)}  => 'magenta', 'magenta bold';

    line qr{(?:tty|/)\d+\s+(\S+)} => 'cyan'      if $using_from;
    line qr{.{$what_pos}(.+)}     => 'cyan bold' if $what_pos != 0;
  }
}

(my $prog, $ENV{PATH}) = new_path;
run( \&process, $prog, @ARGV );


